plugins {
    id "java"
    id "com.diffplug.spotless" version "7.0.2" apply false
}

group = 'com.splunk.public'
description = "Splunk distribution of OpenTelemetry Lambda"

ext {
    versions = [
            opentelemetry: '2.0.0',
            opentelemetryalpha: '2.12.0-alpha'
    ]
}

apply plugin: 'java'
apply plugin: 'com.diffplug.spotless'
apply from: "$rootDir/gradle/spotless.gradle"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into 'build/libs'
}
build.finalizedBy(copyDependencies)

dependencies {
    // OpenTelemetry
    // BOMs
    implementation(platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom-alpha:${versions.opentelemetryalpha}"))

    // API / SDK
    compileOnly("io.opentelemetry:opentelemetry-api")
    compileOnly("io.opentelemetry:opentelemetry-sdk-extension-autoconfigure")
    // AWS
    compileOnly("io.opentelemetry.instrumentation:opentelemetry-aws-lambda-events-2.2")
    // exporters
    compileOnly("io.opentelemetry:opentelemetry-exporter-logging")
    implementation("io.opentelemetry:opentelemetry-exporter-otlp")

    // auto service (loaded by SDK)
    annotationProcessor("com.google.auto.service:auto-service:1.1.1")
    compileOnly("com.google.auto.service:auto-service:1.1.1")

    // AWS lambda
    // events
    implementation("com.amazonaws:aws-lambda-java-events:3.14.0")
    // lambda logging
    implementation("com.amazonaws:aws-lambda-java-log4j2:1.6.0")
    implementation("org.apache.logging.log4j:log4j-slf4j18-impl:2.18.0")
    implementation("org.apache.logging.log4j:log4j-core:2.24.3")
}

// need to exclude "parent" dependencies to avoid clash upon copy / zip of the final layer
configurations.runtimeElements {
    exclude group: 'io.opentelemetry', module: 'opentelemetry-api'
    exclude group: 'io.opentelemetry', module: 'opentelemetry-sdk'
    exclude group: 'io.opentelemetry', module: 'opentelemetry-sdk-trace'
    exclude group: 'io.opentelemetry', module: 'opentelemetry-sdk-common'
    exclude group: 'io.opentelemetry', module: 'opentelemetry-sdk-metrics'
    exclude group: 'io.opentelemetry', module: 'opentelemetry-exporter-otlp-common'
    exclude group: 'io.opentelemetry', module: 'opentelemetry-semconv'
    exclude group: 'org.slf4j', module: 'slf4j-api'
    exclude group: 'com.google.guava'
    exclude group: 'org.jetbrains.kotlin'
    exclude group: 'com.squareup.okhttp3'
    exclude group: 'com.squareup.okio'
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
}

repositories {
    mavenLocal()
    mavenCentral()
}
